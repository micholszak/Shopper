buildscript {
    apply from: "$rootDir/dependencies.gradle"

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath projectPlugins
    }
}

plugins {
    id "com.github.ben-manes.versions" version "$dependency_version"
    id "com.autonomousapps.dependency-analysis" version "$dependency_analysis_version"
    id "org.jlleitschuh.gradle.ktlint" version "$ktlint_plugin_version"
    id "com.osacky.doctor" version "$gradle_doctor_version"
}

allprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}
