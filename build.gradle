buildscript {
    ext {
        kotlin_version = '1.3.72'
        detect_version = '1.10.0'
    }

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.0.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.28-alpha'
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "$detect_version"
}

allprojects {
    apply from: "$rootDir/detect.gradle"
    repositories {
        google()
        jcenter()
    }
}

tasks.detekt.jvmTarget = "1.8"

task clean(type: Delete) {
    delete rootProject.buildDir
}

task detectAll(type: io.gitlab.arturbosch.detekt.Detekt) {
    description = "Runs over whole code base without the starting overhead for each module."
    buildUponDefaultConfig = true
    autoCorrect = true
    parallel = true
    setSource(files(projectDir))
    config.setFrom(files("$rootDir/detekt.yml"))
    include("**/*.kt")
    include("**/*.kts")
    exclude("**/build/**")
    exclude("**/buildSrc/**")
    exclude("**/test/**/*.kt")
    reports {
        xml.enabled = false
        html.enabled = true
        txt.enabled = false
    }
}